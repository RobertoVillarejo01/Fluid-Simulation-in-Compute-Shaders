#version 430

struct Particle
{
    vec3  position;
    float density_constraint;
    vec4  debug_color;
    vec3  velocity;
    float lambda;
    vec3  tentative_position;
    uint  cell_offset;
};
struct ParticleCellOut {
    uint count;
    uint offset;
    uint _pad1;
    uint _pad2;
};



layout (std140, binding = 0) buffer inputFluidData
{
    Particle data[];
} inParticles;

layout (std140, binding = 1) buffer outputCellData
{
    uvec4 offsets;
    ParticleCellOut data[];
} outCells;

layout (local_size_x = 128) in;
layout (location = 0) uniform float mKernelRadius;
layout (location = 1) uniform uint  mParticleCount;
layout (location = 2) uniform vec3  mGravityForce;
layout (location = 3) uniform float mParticleMass;
layout (location = 4) uniform uint  mCellCount;

const float dt = 0.016f;

// A function to compute where this instance should start and end 
// in an array of length "total_size" so that its evenly distributed 
// for the whole workgroup
void ComputeBoundary(uint total_size, out uint start, out uint end)
{
    // There will be partitions with 1 extra elements, this will be placed atan
    // the start. As an example, picture trying to divide 27 elements in 8 boxes:
    // |4|4|4|3|3|3|3|3|
    // Where elements 0 to 3 are in the first box, 4 to 7 in the second...

    // The quot will be the number of elements per partition by default and the quot
    // the number of partitions with one extra element
    // quot = 27/8 = 3;        
    // rest = 27%8 = 3;
    uint quot = total_size / (gl_WorkGroupSize.x * gl_NumWorkGroups.x);
    uint rest = total_size % (gl_WorkGroupSize.x * gl_NumWorkGroups.x);
    uint curr = gl_GlobalInvocationID.x;
    
    // Also, picture we are trying to compute the values for the cell 3:
    // |4|4|4|3|3|3|3|3|
    //      ^            
    // If all cell with extra elements have already been filled
    if (curr >= rest) {
        start = rest * (quot + 1) + (curr - rest) * quot;
        end = start + quot;
    }
    // If there are cells left to fill
    else {
        start = curr * (quot + 1);
        end = start + quot + 1;
    }
}
ivec3 CellCoords(in vec3 pos)
{
    return ivec3(floor(pos / mKernelRadius));
}
uint CellIdx(in ivec3 coords)
{
    return (coords.z * 73856093 +
            coords.y * 83492791 +
            coords.x * 19349663) % mCellCount;
}

void ApplyAcceleration(inout Particle p)
{
    // This is a uniform constant, so it could be computed once or just 
    // uploaded as another uniform
    vec3 acc = mGravityForce / mParticleMass;   
    
    p.velocity += acc * dt;
    p.tentative_position = p.position + p.velocity * dt;
}

void main(void)
{
    uint start, end;
    ComputeBoundary(mParticleCount, start, end);
    for (uint i = start; i < end; ++i) 
    {
        // Compute the new position based on the force
        Particle p = inParticles.data[i];
        ApplyAcceleration(p);

        // Compute the idx in the cell array based on the position of the particle
        uint idx = CellIdx(CellCoords(p.tentative_position));

        // Increase the number of occurences
        p.cell_offset = atomicAdd(outCells.data[idx].count, 1);
        inParticles.data[i] = p;
    }
}
